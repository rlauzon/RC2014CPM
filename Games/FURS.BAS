1 GOSUB 3000
2 DIM F(4)
3 INPUT "ENTER A NUMBER BETWEEN 1 AND 100...";XXX
15 GOSUB 1091
16 I=600
17 PRINT "DO YOU WISH TO TRADE FURS?"
18 GOSUB 1402
19 IF B$="YES" THEN 100
20 IF B$="NO" THEN 2200
23 GOTO 18
25 GOSUB 1402
100 GOSUB 3000
101 X=SQR(1)
102 PRINT"YOU HAVE $";I;"SAVINGS, AND A MAXIMUM OF 190 FURS"
103 PRINT"TO BEGIN THE EXPEDITION"
300 PRINT:PRINT"YOUR 190 FURS ARE DISTRIBUTED AMONG THE FOLLOWING"
302 PRINT"KINDS OF PELTS: MINK, BEAVER, ERMINE AND FOX."
305 PRINT:PRINT
310 GOSUB 1430
315 RESTORE
330 FOR J=1 TO 4
332 READ B$:PRINT
335 PRINT"HOW MANY ";B$;" PELTS DO YOU HAVE";
338 INPUT F(J)
340 F(0)=F(1)+F(2)+F(3)+F(4)
342 IF F(0)=190 THEN 350
344 IF F(0)>190 THEN 500
348 NEXT J
350 GOSUB 3000
352 GOTO 1100
500 PRINT:PRINT"YOU MAY NOT HAVE THAT MANY FURS!"
502 PRINT"DO NOT TRY TO CHEAT. I CAN ADD. YOU MUST START AGAIN!"
504 GOTO15
508 PRINT:PRINT"DO YOU WANT TO TRADE FURS NEXT YEAR?"
513 GOTO 18
1091 PRINT"YOU ARE THE LEADER OF A FRENCH FUR TRADING EXPEDITION IN 1776"
1092 PRINT"LEAVING THE LAKE ONTARIO AREA TO SELL FURS AND GET SUPPLIES FOR"
1093 PRINT"THE NEXT YEAR.  YOU HAVE A CHOICE OF THREE FORTS AT WHICH YOU"
1094 PRINT"MAY TRADE.  THE COST OF SUPPLIES AND THE AMOUNT YOU WILL RECEIVE"
1095 PRINT"FOR YOUR FURS WILL DEPEND ON THE FORT THAT YOU CHOOSE."
1099 RETURN
1100 PRINT:PRINT"DO YOU WANT TO TRADE YOUR FURS AT FORT 1,FORT 2,OR FORT 3 ?"
1102 PRINT"FORT 1 IS HOCHELAGA (MONTREAL), AND IS UNDER THE PROTECTION OF"
1103 PRINT"THE FRENCH ARMY.  FORT 2 IS FORT STADACONA (QUEBEC) AND IS UNDER"
1104 PRINT"THE PROTECTION OF THE FRENCH ARMY, HOWEVER YOU MUST MAKE A"
1105 PRINT"PORTAGE AND CROSS THE LACHINE RAPIDS."
1106 PRINT"FORT 3 IS FORT NEW YORK AND IS UNDER DUTCH CONTROL, YOU MUST"
1108 PRINT"CROSS THROUGH IROQUOIS LAND."
1110 PRINT:PRINT"ANSWER 1, 2, OR 3."
1111 INPUT B
1112 IF B=1 THEN 1120
1113 IF B=2 THEN 1135
1115 IFB=3 THEN 1147
1116 GOTO 111O
1120 PRINT"YOU HAVE CHOSEN THE EASIEST ROUTE. HOWEVER THE FORT IS FAR"
1121 PRINT"FROM ANY SEAPORT. THE VALUE YOU RECEIVE FOR YOUR FURS WILL BE"
1122 PRINT"LOW AND THE COST OF SUPPLIES HIGHER THAN AT FORTS STADACONA"
1123 PRINT"OR NEW YORK.":
1125 GOSUB 1400
1129 IF B$="YES" THEN 1110
1130 GOTO 1160
1135 PRINT:PRINT"YOU HAVE CHOSEN A HARD ROUTE. IT IS,IN COMPARISON,"
1136 PRINT"HARDER THAN THE ROUTE TO HOCHELAGA BUT EASIER THAN THE"
1137 PRINT"ROUTE TO NEW YORK.  YOU WILL RECEIVE AN AVERAGE VALUE FOR"
1138 PRINT"YOUR FURS AND THE COST OF YOUR SUPPLIES WILL BE AVERAGE."
1144 GOSUB 1400
1145 GOSUB 3000
1146 GOTO 1198
1147 PRINT"YOU HAVE CHOSEN THE MOST DIFFICULT ROUTE.  AT FORT NEW YORK"
1148 PRINT"YOU WILL RECEIVE THE HIGHEST VALUE FOR YOUR FURS. THE COST"
1149 PRINT"OF YOUR SUPPLIES WILL BE LOWER THAN AT ALL THE OTHER FORTS."
1152 GOSUB 1400
1155 IF B$="YES" THEN 1110
1156 GOTO 1250
1160 I=I-160
1169 PRINT
1174 M1=INT((.2*RND(XXX)+.7)*100+5)/100
1175 E1=INT((.2*RND(XXX)+.65)*100+.5)/100
1176 B1=INT((.2*RND(XXX)+.75)*100+.5)/100
1177 D1=INT((.2*RND(XXX)+.80)*100+.5)/100
1180 PRINT"SUPPLIES AT FORT HOCHELGA COST $150.00"
1181 PRINT"YOUR TRAVEL EXPENSES TO HOCHELGA WERE $10.00"
1190 GOTO 1410
1198 I=I-140
1201 PRINT
1205 M1=INT((.3*RND(XXX)+.85)*10^2+.5)/10^2
1206 E1=INT((.15*RND(XXX)+.8)*10^2+.75)/10^2
1207 B1=INT((.2*RND(XXX)+.90)*10^2+.5)/10^2
1208 D1=INT((.25*RND(XXX)+.95)*10^2+.5)/10^2
1209 P=INT(10*RND(XXX))+1
1210 IF P<=2 THEN 1216
1212 IF P<=6 THEN 1224
1213 IF P<=8 THEN 1226
1215 IF P<=10 THEN 1235
1216 F(2)=0
1218 PRINT"YOUR BEAVER WERE TOO HEAVY TO CARRY ACROSS THE PORTAGE."
1219 PRINT"YOU HAD TO LEAVE THE PELTS, BUT FOUND THEM STOLEN"
1220 PRINT"WHEN YOU RETURNED FOR THEM."
1221 GOSUB 1244
1222 GOTO 1414
1224 PRINT"YOU ARRIVED SAFELY AT FORT STADACONA"
1225 GOTO 1239
1226 GOSUB 1430
1230 PRINT"YOUR CANOE UPSET IN THE LACHINE RAPIDS. YOU LOST ALL YOUR FURS"
1232 GOSUB 1244
1233 GOTO 1418
1235 P(4)=0
1237 PRINT"YOUR FOX PELTS WERE NOT CURED PROPERLY. NO ONE WILL BUY THEM."
1239 GOSUB 1244
1240 GOTO 1410
1244 PRINT"SUPPLIES AT FORT STADACONA COST $125.00"
1246 PRINT"YOUR TRAVEL EXPENSES TO STADACONA WERE $15.00"
1248 RETURN
1250 I=I-105
1254 PRINT
1260 M1=INT((.15*RND(XXX)+1.05)*10^2+.5)/10^2
1261 E1=INT((.15*RND(XXX)+.95)*10^2+.5)/10^2
1262 B1=INT((.25*RND(XXX)+1.00)*10^2+.5)/10^2
1263 D1=INT((.25*RND(XXX)+1.10)*10^2+.5)/10^2
1270 P=INT(10*RND(0))+1
1271 IF P<=2 THEN 1281
1272 IF P<=6 THEN 1291
1273 IF P<=8 THEN 1295
1274 IF P<=10 THEN 1306
1281 PRINT"YOU WERE ATTACKED BY A PARTY OF IROQUOIS."
1282 PRINT"EVERYONE IN YOUR TRADING GROUP WAS KILLED. THIS ENDS THE GAME."
1284 STOP
1291 PRINT"YOU WERE LUCKY. YOU ARRIVED SAFELY AT FORT NEW YORK."
1293 GOTO 1311
1295 GOSUB 1430
1300 PRINT"YOU NARROWLY ESCAPED AN IROQUOIS RAIDING PARTY, HOWEVER YOU"
1301 PRINT"HAD TO LEAVE ALL YOUR FURS BEHIND."
1303 GOSUB 1320
1304 GOTO 1418
1306 B1=B1/2
1307 M1=M1/2
1308 PRINT"YOUR MINK AND BEAVER WERE DAMAGED ON YOUR TRIP."
1309 PRINT"YOU RECEIVE ONLY HALF THE CURRENT PRICE FOR THESE FURS."
1311 GOSUB 1320
1312 GOTO 1410
1320 PRINT"SUPPLIES AT NEW YORK COST $80.00"
1322 RETURN 
1400 PRINT"DO YOU WANT TO TRADE AT ANOTHER FORT?"
1402 PRINT"ANSWER YES OR NO",
1403 INPUT B$
1404 RETURN
1410 PRINT
1412 PRINT"YOUR BEAVER SOLD FOR $";B1*F(2);
1414 PRINT"YOUR FOX SOLD FOR $";D1*F(4)
1416 PRINT"YOUR ERMINE SOLD FOR $";E1*F(3);
1417 PRINT"YOUR MINK SOLD FOR $";M1*F(1)
1418 I=M1*F(1)+B1*F(2)+E1*F(3)+D1*F(4)+I
1420 PRINT
1422 PRINT"YOU NOW HAVE $";I;" INCLUDING YOUR PREVIOUS SAVINGS"
1425 GOTO 508
1430 FOR J=1 TO 4
1432 F(J)=0
1434 NEXT J
1436 RETURN
2000 DATA "MINK","BEAVER","ERMINE","FOX"
2200 PRINT:PRINT:PRINT"YOU ENDED YOUR FUR TRADING WITH $";I;"."
2210 PRINT"HOPE YOU ENJOYED YOURSELF!"
2500 GOTO 4000
3000 OUT 2,26
3020 RETURN
4000 END